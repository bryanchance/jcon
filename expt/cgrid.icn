#  reads "hue" names from stdin
#  displays color grid showing lightness & saturation variations

$define WIDTH 500
$define HEIGHT 300

procedure main(args)
   local lgts, sats, cw, ch, i, j, hue, colr

   &window := open(&progname, "g", "width=" || WIDTH, "height=" || HEIGHT)

   lgts := ["pale", "light", "medium", "dark", "deep"]
   sats := ["weak", "moderate", "strong", "vivid"]

   cw := (WIDTH + *sats - 1) / *sats
   ch := (HEIGHT + *lgts - 1) / *lgts

   repeat {
      writes("hue: ")
      flush(&output)
      hue := trim(read()) | break
      if *hue = 0 then
	 next
      if hue == "exit" then
	 break
      EraseArea()
      if not Fg(hue) then {
         write("[FAILED]")
	 next
         }
      every i := 1 to *sats do {
         every j := 1 to *lgts do {
	    colr := lgts[j] || " " || sats[i] || " " || hue
	    Fg(colr | "reddish magenta")
	    FillRectangle(cw * (i - 1), ch * (j - 1), cw, ch)
	    }
         }
      }
end
