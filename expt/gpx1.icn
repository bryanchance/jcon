#SRC: JCON

#  gpx1.icn -- a first test of miscellaneous graphics functions


procedure main()
   local colors, a, i, w, h, e, x, y, w2, w3, w4, d, star

   &window := open("window", "g", "size=500,300")
   DrawRectangle(10, 10, 480, 280)

   every i := 3 to 50 by 3 do
      DrawRectangle(0,0,i,i)

   d := [0, 1, -1, 2, -2, 3, -3]
   x := 125
   y := 150
   every 1 to 100 do
       DrawPoint(x +:= ?d, y +:= ?d)

   x := 375
   y := 150
   a := []
   every 1 to 100 do
      put(a, x +:= ?d, y +:= ?d)
   DrawLine ! a

   every i := 5 to 360 by 10 do {
      a := dtor(i)
      w := 75 * cos(a)
      h := 75 * sin(a)
      DrawRectangle(250+w-2, 150+h-2, 4, 4)
      DrawRectangle(250, 150, w, h)
      DrawLine(250 + w, 150 + h, 250 + 100 * cos(a), 150 + 100 * sin(a))
   }

   a := -&pi / 2
   star := []
   every 1 to 5 do
      put(star, 15, a +:= 4 * &pi / 5)
   every FillPolygon ! cartes(star, 470 to 390 by -40, 35)
   DrawPolygon ! cartes(star, 430, 265)

   colors := ["black", "blue", "brown", "cyan", "gray", "green",
      "grey", "magenta", "orange", "pink", "purple", "red",
      "violet", "white", "yellow"]

   every i := 1 to *colors do {
      Fg(colors[i])
      FillRectangle(485, 300 - 15 * i, -15, -15)
   }

   every i := 4 to 360 by 4 do {
      a := dtor(i)
      x := 250 + 180 * cos(a)
      y := 150 + 110 * sin(a)
      Fg(?colors)
      FillRectangle(x - 8, y - 8, 15, 15)
   }

   Fg("magenta")
   Bg("violet")
   w2 := Clone()
   w3 := Clone()
   w4 := Clone()
   Fg(&window, "red")
   Fg(w2, "green")
   Fg(w3, "blue")
   FillRectangle(&window, 15, 270, 15, 15)
   FillRectangle(w2, 30, 270, 15, 15)
   FillRectangle(w3, 45, 270, 15, 15)
   FillRectangle(w4, 60, 270, 15, 15)

   Bg("black")
   EraseArea(15, 250, 15, 15)
   Bg(w2, "brown")
   EraseArea(w2, 30, 250, 15, 15)
   EraseArea(w3, 45, 250, 15, 15)

   while e := Event(&window) do {
      write(image(e))
      if e === !"\^c\^d\dqQ" then break
   }
end



#  cartes(L, x, y) -- return list of cartesian coordinates centered at (x, y)
#			corresponding to list L of polar coordinates

procedure cartes(L, x, y)
   local p, r, a, i
   p := []
   every i := 1 to *L by 2 do {
      r := L[i]
      a := L[i+1]
      put(p, x + r * cos(a))
      put(p, y + r * sin(a))
      }
   return p
end
