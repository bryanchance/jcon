#SRC: JCON

#  keywords.icn -- print keywords and environment variables.

procedure main()

   kw("version", &version)
   every kw("features", &features)
   write()

   kw("host", &host)
   kw("dateline", &dateline)
   kw("date", &date)
   kw("clock", &clock)
   kw("time", &time)
   write()

   kw("null", &null)
   kw("e", &e)
   kw("phi", &phi)
   kw("pi", &pi)
   kw("random", &random)
   write()

   kw("digits", &digits)
   kw("lcase", &lcase)
   kw("ucase", &ucase)
   kw("letters", &letters)
   kw("ascii", *&ascii || " elements")
   kw("cset", *&cset || " elements")
   write()

   kw("progname", &progname)
   kw("file", &file)
   kw("line", &line)
   kw("level", &level)
   kw("trace", &trace)

   write()
   write("environment sampler:")
   every env("USER" | "HOME" | "SHELL" | "EDITOR" | "TERM" | "PRINTER")
end

procedure kw(label, value)
   write(right("&" || label, 10), ": ", value)
   return
end

procedure env(key)
   write(right(key, 9), " = ", getenv(key) | "")
   return
end
