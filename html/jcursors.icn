#  produce mouse pointer display for Jcon documentation
#  (must be run by v9 Icon!)
#
#  10-aug-98/gmt
#  <<ARIZONA-ONLY>>
#
#  assumes that X cursor list assignments match the list below


$define XOFF 40
$define YOFF 20
$define DY 50

$define CX -20
$define CY 10


link graphics

global cwin, x, y

procedure main(args)
   Window("size=440,300", "bg=white", args)
   VSetFont()
   cwin := Clone("font=cursor") | stop("can't get cursor font")

   column(0)
   cursor( 68, "arrow", "left ptr")
   cursor( 34, "cross", "crosshair")
   cursor( 60, "hand")
   cursor( 52, "move", "fleur")
   cursor(152, "text", "ibeam", "xterm")
   cursor(150, "wait", "watch")

   column(120)
   cursor( 70, "left side")
   cursor(138, "top side")
   cursor( 96, "right side")
   cursor( 16, "bottom side")

   column(250)
   cursor(134, "top left corner")
   cursor(136, "top right corner")
   cursor( 14, "bottom right corner")
   cursor( 12, "bottom left corner")

   ZDone()
end

procedure column(dx)
   local dy

   dx := "dx=" || (dx + XOFF)
   dy := "dy=" || YOFF
   WAttrib(dx, dy)
   WAttrib(cwin, dx, dy)
   return
end

procedure cursor(n, s1, s2, s3)
   DrawString(cwin, CX, CY, char(n))
   GotoRC(1, 1)
   every WWrite(s1 | \s2 | \s3)
   Translate(0, DY)
   Translate(cwin, 0, DY)
   return
end
