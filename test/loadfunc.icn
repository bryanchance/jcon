#SRC: JCON

#  test dynamic loading, especially examples from the doc

global glob0
invocable "hello1"
# and hello2 is invocable by virtue of ref from iconprocs()



procedure main()
   iconprocs()		# test separately compiled Icon procedures
   javaprocs()		# test separately compiled Java procedures
   genproc()		# generate, build, and test an Icon procedure
end



procedure iconprocs()
   local proc1a, proc1b, proc1c, proc2a, proc2b

   proc1a := loadfunc("load1.zip", "proc1a")
   proc2b := loadfunc("load2.zip", "proc2b")
   proc2a := loadfunc("load2.zip", "proc2a")
   proc1b := loadfunc("load1.zip", "proc1b")
   proc1c := loadfunc("load1.zip", "proc1a")  # 1c is copy of 1a

   hello2("from iconprocs")
   every write(image(proc1a | proc1b | proc1c | proc2a | proc2b))

   every n := 1000 | 2000 do {
      write()
      proc1a(n + 100)
      proc2a(n + 200)
      proc1b(n + 300)
      proc1c(n + 400)
      proc2b(n + 500)
   }

end

procedure hello1(s)
   write("hello1 ", s)
end

procedure hello2(s)
   write("hello2 ", s)
end



procedure javaprocs()
   local sum3, factors, threen, f, i

   sum3 := loadfunc("jfuncs.zip", "sum3")
   factors := loadfunc("jfuncs.zip", "factors")
   threen := loadfunc("jfuncs.zip", "threen")

   write()
   every f := sum3 | factors | threen do
      write(image(f), " : ", args(f), " args")

   write()
   write("sum3: ", sum3(5, 8, 11), " ", sum3(12.0, 23.7, "45.8"))

   write()
   every i := 24 | 30 | 45 | 48 | 60 | 120 do {
       writes("factors(", i, "): ")
       every writes(" ", factors(i))
       write()
   }

   write()
   every i := 1 to 30 do {
       writes("3n+1(", i, "): ")
       every writes(" ", threen(i))
       write()
   }
   return
end



procedure genproc()
   local f, p, i

   remove("tmp.icn")
   remove("tmp.zip")
   f := open("tmp.icn", "w") | stop("can't open tmp.icn")
   wproc(f)
   close(f)
   system("../bin/jcont -s -c tmp")
   p := loadfunc("tmp.zip", "inv")

   write()
   every i := 1 to 25 do 
      write("1/", i, " = ", p(i))
   remove("tmp.zip")
   remove("tmp.icn")
end

procedure wproc(f)
   every write(f, ![
      "procedure inv(x)", 
      "   return adiv(-1.0, x)",
      "end",
      "",
      "procedure adiv(a, b)",
      "   return abs(a) / b",
      "end"
   ]);
end
