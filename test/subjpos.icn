#SRC: JCON

procedure main()
   local i, s

   ws()		# dump initial state
   write()

   every setsubj(s := "cat" | "" | "donut" | "x") do
      every i := -*s-2 to *s+3 do
         setpos(i)
   write()

   "outer" ? {
      ws()
      &pos := 3
      ws()
      "inner" ? {
         ws()
         &pos := 2
         ws()
         }
      ws()
      }
end

procedure setpos(n)
   writes("p := ", right(image(n),10))
   if &pos := n then
      writes("       ")
   else
      writes(" FAILED")
   ws()
   return
end

procedure setsubj(s)
   writes("s := ", right(image(s),10))
   if &subject := s then
      writes("       ")
   else
      writes(" FAILED")
   ws()
   return
end

procedure ws()	# write state of &setpos and &subject
   write("  &pos=", image(&pos), "   &subject=", image(&subject))
   return
end
