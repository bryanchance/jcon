invocable all

procedure predefs()
	local t

	t := table()
	t["_JAVA"] := "1"
	t["_ASCII"] := "1"
	t["_CO_EXPRESSIONS"] := "1"
	t["_SYSTEM_FUNCTION"] := "1"
	# there is no predefn for "environment variables" feature
	return t
end

procedure getline(f)
	if \f then {
		return create preprocessor(f, predefs())
	} else {
		return create !&input
	}
end

procedure main(L) 
	local filename, cg_flags, cg, g, preonly

	cg_flags := []
	while L[1] do {	# process flags
		pop(L) ? {
			if ="-E" then {
				preonly := 1	# preprocess only
			} else if ="-target:" then {
				/CG | stop("ERROR: multiple -target specifications")
				cg := tab(0)
				CG := (\proc(cg || "_CG"))()
				\CG | stop("ERROR: unknown target: ", cg)
			} else if ="-" then {
				put(cg_flags, &subject)
			} else {
				/filename | stop("ERROR: only one file name")
				filename := &subject
			}
		}
	}

	g := getline(filename)

	if \preonly then {
		while write(@g)
	} else {
		\CG | stop("ERROR: must specify -target")
		CG.File(g, cg_flags)
	}

	#every writes(&errout, " ", "  total alloc: " | &allocated | "\n")
	#every writes(&errout, " ", "  current:     " | &storage | "\n")
	#every writes(&errout, " ", "  collections: " | &collections | "\n")
end
